;*******************************************************************************
; Serial Communication interface
;*******************************************************************************

;*******************************************************************************
; STRINGS
;*******************************************************************************

hexakars            fcc       '0123456789ABCDEF'

;*******************************************************************************
; FUNCTIONS
;*******************************************************************************

;*******************************************************************************
; Initialize SCI controller for 57600 bps

                    #spauto

SCIInit             proc
                    clr       SCC3
                    mov       #SCBR_PD_1+SCBR_BD_1,SCBR
                    mov       #$26,SCPSC          ; PDS(1)+PSSB(6)

                    mov       #ENSCI_,SCC1
                    mov       #TE_+RE_,SCC2
                    rts

;*******************************************************************************
; Purpose: Convert a binary number to its hex ASCII equivalent
; Input  : A = binary number (0..15)
; Output : A = ASCII equivalent ('0'..'9','A'..'F')
; Note(s):
                    #spauto

ToHexASCII          proc
                    and       #$0F
                    add       #$90
                    daa
                    adc       #$40
                    daa
                    rts

;*******************************************************************************
; Prints a byte in hexa format from A

                    #spauto

sciputb             proc
                    psha      ch@@
                    pshhx
                    nsa                           ; Upper nibble
                    bsr       ToHexASCII
                    bsr       sciputc
                    lda       ch@@,sp             ; Lower nibble
                    bsr       ToHexASCII
                    bsr       sciputc
                    pull
                    rts

;*******************************************************************************
; Tries to read character from SCI. Carry bit shows if there is received character in A or not.

                    #spauto

scigetc             proc
                    brclr     5,SCS1,Done@@       ; SCRF = 1 ?
                    lda       SCDR
                    sec                           ; RX info in carry bit
Done@@              rts

;*******************************************************************************
; Prints a string. String address is in H:X

                    #spauto

sciputs             proc
Loop@@              lda       ,x
                    beq       Done@@
                    bsr       sciputc
                    aix       #1
                    bra       Loop@@
Done@@              equ       :AnRTS

;*******************************************************************************
; Prints a character from A

                    #spauto

sciputc             proc
Loop@@              bsr       TBMHandle
                    brclr     7,SCS1,Loop@@       ; wait to send out the previous character
                    sta       SCDR                ; also SCTE is cleared here
                    rts

;*******************************************************************************
                    #sp
;*******************************************************************************
